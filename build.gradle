plugins {
    id 'java'
    id 'checkstyle'
    id 'idea'
    id 'jacoco'
    id 'pmd'
    id 'com.github.spotbugs' version "3.0.0"
    id "org.sonarqube" version "2.8"
}

defaultTasks 'build'

allprojects {
    
    description = 'Webhooks4j'
    group = 'dk.jensborch'
    version = '1.0-SNAPSHOT'

    repositories {
        jcenter()
        mavenCentral()
    }    

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8    
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    compileTestJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'pmd'
    apply plugin: 'com.github.spotbugs'
    
    dependencies {
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.2'
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.2.4'
    }
    
    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
    
    jacocoTestReport {
        reports {
            xml.enabled true
            csv.enabled false
            html.enabled false
        }
    }

    pmd {
        ruleSets = []
        ruleSetFiles = files("$rootDir/config/pmd/ruleset.xml")
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("$buildDir/reports/jacoco/test/jacocoTestReport.xml")
        html.enabled false
        csv.enabled false
    }
}